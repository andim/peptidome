ks = list(range(4, 12, 1))

rule all:
    input:
        expand('plots/Human_{model}_k{k}_observables.png',
            model=['train', 'independent', 'ncov', 'nskew', 'nskewfcov'], k=ks),
        expand('plots/Human_k{k}_dos.png', k=ks)

rule train_test:
    output:
        'data/Human_train_k{k}_matrix.csv.gz',
        'data/Human_test_k{k}_matrix.csv.gz',
    script:
       'generate_reference_matrices.py' 

rule fit_independent:
    input:
        'data/Human_train_k{k}_matrix.csv.gz'
    output:
        'data/Human_independent_k{k}_matrix.csv.gz',
        'data/Human_independent_k{k}_params.npz',
    script:
        'fit_independent.py'

rule fit_ncov:
    input:
        'data/Human_train_k{k}_matrix.csv.gz'
    output:
        'data/Human_ncov_k{k}_matrix.csv.gz',
        'data/Human_ncov_k{k}_params.npz',
    script:
        'fit_ncov.py'

rule fit_nskew:
    input:
        'data/Human_train_k{k}_matrix.csv.gz',
        'data/Human_ncov_k{k}_params.npz'
    output:
        'data/Human_nskew_k{k}_matrix.csv.gz',
        'data/Human_nskew_k{k}_params.npz',
    script:
        'fit_nskew.py'

rule fit_nskewfcov:
    input:
        'data/Human_train_k{k}_matrix.csv.gz',
        'data/Human_nskew_k{k}_params.npz'
    output:
        'data/Human_nskewfcov_k{k}_matrix.csv.gz',
        'data/Human_nskewfcov_k{k}_params.npz',
    script:
        'fit_nskewfcov.py'

rule evaluate_observables:
    input:
        'data/Human_{model}_k{k}_matrix.csv.gz'
    output:
        'data/Human_{model}_k{k}_observables.npz'
    script:
        'evaluate_observables.py'

rule evaluate_energies:
    input:
        'data/Human_{model}_k{k}_matrix.csv.gz',
        'data/Human_nskewfcov_k{k}_params.npz'
    output:
        'data/Human_{model}_k{k}_energies.npz'
    script:
        'evaluate_energies.py'

rule observables_plot:
    input:
        'data/Human_test_k{k}_observables.npz',
        'data/Human_{model}_k{k}_observables.npz'
    output:
        'plots/Human_{model}_k{k}_observables.png'
    script:
        'plot_observables.py'

rule dos_plot:
    input:
        expand('data/Human_{model}_k{{k}}_energies.npz',
            model = ['test', 'independent', 'ncov', 'nskew', 'nskewfcov']),
    output:
        'plots/Human_k{k}_dos.png'
    script:
        'plot_dos.py'

rule maintext_plot:
    input:
        'data/Human_test_k9_observables.npz',
        'data/Human_nskewfcov_k9_observables.npz',
        expand('data/Human_{model}_k9_energies.npz',
            model = ['test', 'independent', 'ncov', 'nskew', 'nskewfcov']),
    output:
        'plots/maxent.svg'
    script:
        'plot_maintext.py'
